import com.android.build.gradle.internal.tasks.FinalizeBundleTask

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.google.services)
    alias(libs.plugins.ksp)
    alias(libs.plugins.firebase.appdistribution)
    alias(libs.plugins.room)
}

def SCHEME_BASE = "cryptoxmwallet"
def WC_SCHEME_BASE = "cryptox-wc"
def PROVIDER_AUTHORITY_BASE = "com.pioneeringtechventures.wallet.DataFileProvider"

def configurableSigningProperties = new Properties()
configurableSigningProperties.load(new FileInputStream(
        file(System.getenv("CONFIGURABLE_SIGNING_PROPERTIES_FILE") ?: "dev-signing.properties")
))

android {
    namespace "com.concordium.wallet"

    // This project uses semantic versioning
    // https://semver.org/
    def versionMajor = 1
    def versionMinor = 17
    def versionPatch = 0
    def versionMeta = ""
    def versionCodeIncremental = 1494

    defaultConfig {
        applicationId "com.pioneeringtechventures.wallet"

        //noinspection OldTargetApi
        targetSdk 35
        //noinspection GradleDependency
        compileSdk 35
        minSdk 26

        versionCode versionCodeIncremental
        versionName "${versionMajor}.${versionMinor}.${versionPatch}${versionMeta}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("boolean", "SHOW_GTU_DROP", "false")
        buildConfigField("Long", "ACCOUNT_UPDATE_FREQUENCY_SEC", "60l")
        buildConfigField("Long", "FAST_ACCOUNT_UPDATE_FREQUENCY_SEC", "5l")

        firebaseAppDistribution {
            // This file is created by CI.
            serviceCredentialsFile = file("app-distribution-credentials.json")
        }
    }

    room {
        schemaDirectory "$projectDir/schemas"
    }

    signingConfigs {
        configurable {
            storeFile file(configurableSigningProperties['storeFile'])
            storePassword configurableSigningProperties['storePassword']
            keyAlias configurableSigningProperties['keyAlias']
            keyPassword configurableSigningProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            debuggable false
            signingConfig signingConfigs.configurable
            // Unlocking requires vigilant testing.
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
        flavorDimensions += "network"
    }

    productFlavors {
        // Unstable Concordium test network, fake funds and identities. No Spaceseven instance.
        stagenet {
            dimension "network"
            applicationIdSuffix ".stagenet"
            buildConfigField("String", "EXPORT_CHAIN", "\"staging\"")
            buildConfigField("String", "ENV_NAME", "\"staging\"")
            buildConfigField("String", "URL_PROXY_BASE", "\"https://wallet-proxy.stagenet.concordium.com\"")
            buildConfigField("String", "URL_EXPLORER_BASE", "\"https://stagenet.ccdscan.io/\"")
            buildConfigField("String", "URL_NOTIFICATIONS_BASE", "\"https://notification-api.stagenet.concordium.com/api/\"")
            buildConfigField("String[]", "WC_CHAINS", "{\"ccd:stagenet\",\"ccd:853288fa5a45554d3cbbf8a756b85abc\"}")
            buildConfigField("boolean", "SHOW_GTU_DROP", "true")

            def scheme = SCHEME_BASE + "-stagenet"
            buildConfigField("String", "SCHEME", "\"${scheme}\"")
            resValue "string", "scheme", "\"${scheme}\""
            resValue "string", "wc_scheme", "\"${WC_SCHEME_BASE + "-stagenet"}\""

            def providerAuthority = PROVIDER_AUTHORITY_BASE + "-stagenet"
            resValue "string", "PROVIDER_AUTHORITY", "\"${providerAuthority}\""
            buildConfigField("String", "PROVIDER_AUTHORITY", "\"${providerAuthority}\"")

            // There is no Spaceseven instance for Stagenet.
            // This domain won't resolve.
            buildConfigField("String", "S7_DOMAIN", "\"https://stagenet.spaceseven.cloud\"")

            firebaseAppDistribution {
                appId = '1:124880082147:android:44ec3892cba12e73c7e1d6'
                groups = "concordium-team"
            }
        }

        // Public Concordium test network, fake funds and identities. Spaceseven stage.
        // It is called 'tstnet' because flavour names can't start with 'test'
        tstnet {
            dimension "network"
            applicationIdSuffix ".testnet"
            buildConfigField("String", "EXPORT_CHAIN", "\"testnet\"")
            buildConfigField("String", "ENV_NAME", "\"testnet\"")
            buildConfigField("String", "URL_PROXY_BASE", "\"https://wallet-proxy.testnet.concordium.com\"")
            buildConfigField("String", "URL_EXPLORER_BASE", "\"https://testnet.ccdscan.io/\"")
            buildConfigField("String", "URL_NOTIFICATIONS_BASE", "\"https://notification-api.testnet.concordium.com/api/\"")
            buildConfigField("String[]", "WC_CHAINS", "{\"ccd:testnet\",\"ccd:4221332d34e1694168c2a0c0b3fd0f27\"}")
            buildConfigField("boolean", "SHOW_GTU_DROP", "true")
            def scheme = SCHEME_BASE + "-testnet"
            buildConfigField("String", "SCHEME", "\"${scheme}\"")
            resValue "string", "scheme", "\"${scheme}\""
            resValue "string", "wc_scheme", "\"${WC_SCHEME_BASE + "-testnet"}\""
            def providerAuthority = PROVIDER_AUTHORITY_BASE + "-testnet"
            resValue "string", "PROVIDER_AUTHORITY", "\"${providerAuthority}\""
            buildConfigField("String", "PROVIDER_AUTHORITY", "\"${providerAuthority}\"")

            // Yes, stage. Spaceseven stage runs on Concordium Testnet.
            buildConfigField("String", "S7_DOMAIN", "\"https://stage.spaceseven.cloud\"")
            firebaseAppDistribution {
                appId = '1:124880082147:android:40bb3b4880d6aa92c7e1d6'
                groups = "concordium-team"
            }
        }

        // Public Concordium network, real funds and identities. Spaceseven production.
        mainnet {
            dimension "network"
            buildConfigField("String", "EXPORT_CHAIN", "\"mainnet\"")
            buildConfigField("String", "ENV_NAME", "\"production\"")
            buildConfigField("String", "URL_PROXY_BASE", "\"https://wallet-proxy.mainnet.concordium.software\"")
            buildConfigField("String", "URL_EXPLORER_BASE", "\"https://ccdscan.io/\"")
            buildConfigField("String", "URL_NOTIFICATIONS_BASE", "\"https://notification-api.mainnet.concordium.software/api/\"")
            buildConfigField("String[]", "WC_CHAINS", "{\"ccd:mainnet\",\"ccd:9dd9ca4d19e9393877d2c44b70f89acb\"}")
            buildConfigField("boolean", "SHOW_GTU_DROP", "false")

            def scheme = SCHEME_BASE
            buildConfigField("String", "SCHEME", "\"${scheme}\"")
            resValue "string", "scheme", "\"${scheme}\""
            resValue "string", "wc_scheme", "\"${WC_SCHEME_BASE}\""

            def providerAuthority = PROVIDER_AUTHORITY_BASE
            resValue "string", "PROVIDER_AUTHORITY", "\"${providerAuthority}\""
            buildConfigField("String", "PROVIDER_AUTHORITY", "\"${providerAuthority}\"")
            buildConfigField("String", "S7_DOMAIN", "\"https://spaceseven.com\"")

            firebaseAppDistribution {
                appId = '1:124880082147:android:250469ea97158747c7e1d6'
                groups = "concordium-team"
            }
        }
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    packagingOptions {
        resources {
            excludes += [
                    'META-INF/atomicfu.kotlin_module',
                    'META-INF/INDEX.LIST',
                    'META-INF/DEPENDENCIES',
                    'META-INF/LICENSE.md',
                    'META-INF/NOTICE.md',
                    'META-INF/versions/**'
            ]
        }
        jniLibs {
            keepDebugSymbols += ['**/*.so']
        }
    }

    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }

    applicationVariants.all { variant ->
        def flavor = variant.productFlavors[0].name
        def artifactName = "concordium-wallet-" +
                flavor + "-" +
                variant.buildType.name + "-" +
                variant.versionName

        // APK.
        variant.outputs.all {
            outputFileName = artifactName + ".apk"
        }

        // AAB.
        tasks.named("sign${variant.name.capitalize()}Bundle", FinalizeBundleTask) {
            File file = finalBundleFile.asFile.get()
            finalBundleFile.set(new File(file.parentFile, "${artifactName}.aab"))
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation("com.concordium.sdk:concordium-android-sdk:12.0.0-alpha.2") {
        exclude group: "org.bouncycastle"
        exclude group: "net.jcip"
    }

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation libs.kotlin.stdlib
    implementation libs.kotlin.reflect
    api libs.kotlinx.coroutines.android

    // Glide
    implementation libs.glide
    ksp libs.glide.ksp
    implementation libs.androidsvg

    implementation "com.github.komputing.khash:sha256:1.1.1"

    // AndroidX
    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.recyclerview
    implementation libs.material
    implementation libs.androidx.cardview
    implementation libs.androidx.biometric
    implementation libs.flexbox

    // Lifecycles
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.lifecycle.livedata.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation libs.androidx.fragment.ktx

    // Room
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx
    ksp libs.androidx.room.compiler
    androidTestImplementation "androidx.room:room-testing:${libs.versions.room.get()}"

    // Barcode
    implementation libs.zxing.android

    // Chrome custom tabs
    implementation 'androidx.browser:browser:1.4.0'

    implementation 'com.github.Redman1037:TSnackBar:V2.0.0'
    implementation 'com.github.Dimezis:BlurView:version-2.0.6'

    // Networking
    implementation libs.okhttp.logging
    implementation('com.github.ihsanbal:LoggingInterceptor:3.1.0') {
        exclude group: 'org.json', module: 'json'
    }
    implementation libs.retrofit
    implementation libs.retrofit.converter.gson
    implementation libs.retrofit.converter.scalars
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.21'

    // Navigation
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx

    // Crypto library
    implementation project(":mobile_wallet_lib-release")

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "android.arch.persistence.room:testing:1.1.1"

    // Reown (formerly WalletConnect)
    implementation(platform "com.reown:android-bom:1.0.4")
    implementation "com.reown:android-core"
    implementation "com.reown:sign"

    // Scarlet Gson adapter required for Spaceseven wallet connection.
    implementation("com.tinder.scarlet:message-adapter-gson:0.1.12") {
        // Core must be excluded to avoid class duplication when using WalletConnect.
        exclude group: 'com.tinder.scarlet', module: 'scarlet-core'
    }

    implementation libs.eventbus

    // BIP39
    implementation "cash.z.ecc.android:kotlin-bip39:1.0.4"

    // Firebase
    implementation 'com.google.firebase:firebase-messaging:24.0.0!!'
    implementation 'com.google.android.gms:play-services-location:21.3.0'

    // Room annotation processor
    ksp libs.androidx.room.compiler

    implementation libs.lottie
    implementation libs.androidx.swiperefreshlayout

    // In-app rating
    implementation 'com.google.android.play:review:2.0.2'
    implementation 'com.google.android.play:review-ktx:2.0.2'

    // Dot indicator for pagers.
    implementation 'com.tbuonomo:dotsindicator:4.3!!'

    annotationProcessor 'org.projectlombok:lombok:1.18.38'
}

task printVersionName {
    group = "help"
    doLast {
        println android.defaultConfig.versionName
    }
}
