import com.android.build.gradle.internal.tasks.FinalizeBundleTask

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.google.services)
    alias(libs.plugins.ksp)
    alias(libs.plugins.firebase.appdistribution)
    alias(libs.plugins.room)
}

def SCHEME_BASE = "cryptoxmwallet"
def PROVIDER_AUTHORITY_BASE = "com.pioneeringtechventures.wallet.DataFileProvider"

def configurableSigningProperties = new Properties()
configurableSigningProperties.load(new FileInputStream(
        file(System.getenv("CONFIGURABLE_SIGNING_PROPERTIES_FILE") ?: "dev-signing.properties")
))

android {
    namespace "com.concordium.wallet"

    // This project uses semantic versioning
    // https://semver.org/
    def versionMajor = 1
    def versionMinor = 18
    def versionPatch = 0
    def versionMeta = ""
    def versionCodeIncremental = 1498

    defaultConfig {
        applicationId "com.pioneeringtechventures.wallet"

        //noinspection OldTargetApi
        targetSdk 35
        //noinspection GradleDependency
        compileSdk 35
        minSdk 26

        versionCode versionCodeIncremental
        versionName "${versionMajor}.${versionMinor}.${versionPatch}${versionMeta}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("Long", "ACCOUNT_UPDATE_FREQUENCY_SEC", "60l")
        buildConfigField("Long", "FAST_ACCOUNT_UPDATE_FREQUENCY_SEC", "5l")

        firebaseAppDistribution {
            // This file is created by CI.
            serviceCredentialsFile = file("app-distribution-credentials.json")
        }
    }

    room {
        schemaDirectory "$projectDir/schemas"
    }

    signingConfigs {
        configurable {
            storeFile file(configurableSigningProperties['storeFile'])
            storePassword configurableSigningProperties['storePassword']
            keyAlias configurableSigningProperties['keyAlias']
            keyPassword configurableSigningProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            debuggable false
            signingConfig signingConfigs.configurable
            // Unlocking requires vigilant testing.
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
        flavorDimensions += "channel"
    }

    productFlavors {
        // Stable channel – tested and ready for Mainnet.
        stable {
            dimension "channel"

            // Default network for the stable build is Mainnet.
            // Must not be changed for backward compatibility.
            buildConfigField("String", "DEFAULT_NETWORK_GENESIS_HASH", "\"9dd9ca4d19e9393877d2c44b70f89acbfc0883c2243e5eeaecc0d1cd0503f478\"")

            def scheme = SCHEME_BASE
            buildConfigField("String", "SCHEME", "\"${scheme}\"")
            resValue "string", "scheme", "\"${scheme}\""

            def providerAuthority = PROVIDER_AUTHORITY_BASE
            resValue "string", "PROVIDER_AUTHORITY", "\"${providerAuthority}\""
            buildConfigField("String", "PROVIDER_AUTHORITY", "\"${providerAuthority}\"")

            firebaseAppDistribution {
                appId = '1:124880082147:android:250469ea97158747c7e1d6'
                groups = "concordium-team"
            }
        }

        // Preview channel – unstable features or features not yet live on Mainnet.
        preview {
            dimension "channel"

            // Default network for the preview build is Testnet.
            // Must not be changed for backward compatibility.
            buildConfigField("String", "DEFAULT_NETWORK_GENESIS_HASH", "\"4221332d34e1694168c2a0c0b3fd0f273809612cb13d000d5c2e00e85f50f796\"")

            // Keep "-testnet" for backward compatibility, used to be a Testnet wallet.
            applicationIdSuffix ".testnet"

            def scheme = SCHEME_BASE + "-preview"
            buildConfigField("String", "SCHEME", "\"${scheme}\"")
            resValue "string", "scheme", "\"${scheme}\""

            def providerAuthority = PROVIDER_AUTHORITY_BASE + "-preview"
            resValue "string", "provider_authority", "\"${providerAuthority}\""
            buildConfigField("String", "PROVIDER_AUTHORITY", "\"${providerAuthority}\"")

            firebaseAppDistribution {
                appId = '1:124880082147:android:40bb3b4880d6aa92c7e1d6'
                groups = "concordium-team"
            }
        }
    }

    packagingOptions {
        resources {
            excludes += [
                    'META-INF/atomicfu.kotlin_module',
                    'META-INF/INDEX.LIST',
                    'META-INF/DEPENDENCIES',
                    'META-INF/LICENSE.md',
                    'META-INF/NOTICE.md',
                    'META-INF/versions/**'
            ]
        }
        jniLibs {
            keepDebugSymbols += ['**/*.so']
        }
    }

    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }

    applicationVariants.all { variant ->
        def flavor = variant.productFlavors[0].name
        def artifactName = "concordium-wallet-" +
                flavor + "-" +
                variant.buildType.name + "-" +
                variant.versionName

        // APK.
        variant.outputs.all {
            outputFileName = artifactName + ".apk"
        }

        // AAB.
        tasks.named("sign${variant.name.capitalize()}Bundle", FinalizeBundleTask) {
            File file = finalBundleFile.asFile.get()
            finalBundleFile.set(new File(file.parentFile, "${artifactName}.aab"))
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":mobile_wallet_lib-release")

    implementation(libs.concordium.android.sdk) {
        exclude group: "org.bouncycastle"
        exclude group: "net.jcip"
    }

    implementation libs.kotlin.stdlib
    implementation libs.kotlin.reflect
    api libs.kotlinx.coroutines.android

    implementation libs.glide
    ksp libs.glide.ksp
    implementation libs.androidsvg
    implementation libs.khash.sha256

    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.recyclerview
    implementation libs.material
    implementation libs.androidx.cardview
    implementation libs.androidx.biometric
    implementation libs.flexbox

    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.lifecycle.livedata.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.legacy.support
    implementation libs.androidx.fragment.ktx

    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx
    ksp libs.androidx.room.compiler
    androidTestImplementation libs.androidx.room.testing

    implementation libs.zxing.android
    implementation libs.androidx.browser
    implementation libs.snackbar
    implementation libs.blur.view

    implementation libs.okhttp.logging
    implementation(libs.logging.interceptor) {
        exclude group: 'org.json', module: 'json'
    }
    implementation libs.retrofit
    implementation libs.retrofit.converter.gson
    implementation libs.retrofit.converter.scalars

    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx

    testImplementation libs.test.junit
    androidTestImplementation libs.test.ext.junit
    androidTestImplementation libs.test.espresso.core
    androidTestImplementation libs.test.arch.core
    testImplementation libs.test.room

    implementation(platform libs.reown.bom)
    implementation libs.reown.core
    implementation libs.reown.sign
    implementation libs.eventbus
    implementation libs.kotlin.bip39
    implementation libs.firebase.messaging
    implementation libs.play.services
    implementation libs.lottie
    implementation libs.androidx.swiperefreshlayout
    implementation libs.play.review
    implementation libs.play.review.ktx
    implementation libs.dotsindicator
    annotationProcessor libs.lombok.processor
}

task printVersionName {
    group = "help"
    doLast {
        println android.defaultConfig.versionName
    }
}
